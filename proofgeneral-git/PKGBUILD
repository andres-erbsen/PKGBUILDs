# Maintainer: William J Bowman <aur@williamjbowman.com>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>
# Contributor: dimino jeremie <jeremie@dimino.org>
# Contributor: Andres Erbsen <andreser@mit.edu>

pkgname=proofgeneral-git
pkgver=4.3pre.8632
pkgver() {
  cd "$srcdir/PG"
  echo "$(grep -oP '(?<=PRERELEASE_TAG=)[\.0-9]*(pre)?' Makefile.devel).$(git log --oneline | wc -l)"
}
pkgrel=1
pkgdesc='Generic interface for proof assistants -- git version.'
arch=('i686' 'x86_64')
license=('GPL')
url='http://proofgeneral.inf.ed.ac.uk/'
depends=('emacs>=24.3')
conflicts=('proofgeneral')
install="${pkgname}.install"
source=("git://github.com/ProofGeneral/PG")

sha512sums=('SKIP')

build() {
  cd "$srcdir/PG"
  make
}

package() {
  cd "$srcdir/PG"

  make PREFIX=${pkgdir}/usr install -j1
  chmod +x "${pkgdir}/usr/bin/proofgeneral"
}

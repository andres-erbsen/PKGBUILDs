# Maintainer: Andres Erbsen <andres@krutt.org>

pkgname=pond-git
pkgrel=1
pkgver=423.d3f5a23
pkgdesc="Forward-Secure asynchronous messaging"
url="https://pond.imperialviolet.org/"
arch=('x86_64' 'i686')
license=('BSD') # 3-clause
depends=('hunspell-en' 'tor' 'gtkspell3')
optdepends=(
  'trousers: TPM support'
)
makedepends=('go' 'git' 'mercurial' 'gtkspell3' 'trousers')
_gorepopath='github.com/agl/pond'
_goimportpath="$_gorepopath/client"
source=("$pkgname"::"git+https://$_gorepopath.git")
sha256sums=('SKIP')
options=('!strip' '!emptydirs')

pkgver() {
  cd "$pkgname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir"
  GOPATH="$srcdir" go get -v -x ${_goimportpath}/...
}

package() {
  install -Dm755 "$srcdir/bin/client" "$pkgdir/usr/bin/pond"
}

# vim:set ts=2 sw=2 et:

# Maintainer: Andres Erbsen <andres@krutt.org>

pkgname=pond-git
pkgrel=1
pkgver=v0.1.166.g16afb67
pkgdesc="Forward-Secure asynchronous messaging"
url="https://pond.imperialviolet.org/"
arch=('x86_64' 'i686')
license=('BSD') # 3-clause
depends=('hunspell-en' 'tor' 'gtkspell3')
optdepends=(
  'trousers: TPM support'
)
makedepends=('go' 'git' 'mercurial' 'gtkspell3' 'trousers')
_gorepopath='github.com/agl/pond'
_goimportpath="$_gorepopath/client"
source=("$pkgname"::"git+https://$_gorepopath.git")
sha256sums=('SKIP')
options=('!strip' '!emptydirs')

prepare() {
  GOPATH="$srcdir/go"
  mkdir -p "$GOPATH/src/$_gorepopath"
  rm -rf "$GOPATH/src/$_gorepopath"
  cp -r "$srcdir/$pkgname" "$GOPATH/src/$_gorepopath"
}

build() {
  test -x /etc/profile.d/go.sh && source /etc/profile.d/go.sh
  export GOPATH="$srcdir/go"
  cd "$GOPATH/src/$_goimportpath"
  go get -u ./...
  go install
}

package() {
  install -Dm755 "$srcdir/go/bin/client" "$pkgdir/usr/bin/pond"
}

# vim:set ts=2 sw=2 et:

# $Id: PKGBUILD 210195 2014-04-11 09:28:05Z thomas $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=openvpn
pkgver=2.3.3
pkgrel=2
pkgdesc="An easy-to-use, robust, and highly configurable VPN (Virtual Private Network)"
arch=(i686 x86_64)
url="http://openvpn.net/index.php/open-source.html"
depends=('openssl' 'lzo2' 'iproute2')
makedepends=('systemd' 'libsystemd')
license=('custom')
install=openvpn.install
source=(http://swupdate.openvpn.net/community/releases/openvpn-${pkgver}.tar.gz
        http://swupdate.openvpn.net/community/releases/openvpn-${pkgver}.tar.gz.asc
        openvpn@.service
		https://raw.githubusercontent.com/NixOS/nixpkgs/master/pkgs/tools/networking/openvpn/systemd-notify.patch)
md5sums=('5c66ea3143ac884a3075521bd74ede06'
         'SKIP'
         '1132fdf4bce82aafe3860e1146ed27ca'
		 '54597ba53ac6be76db4f09b25e4d7024')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -p1 < ../../systemd-notify.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  LDFLAGS=-lsystemd-daemon CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/lib/openvpn\\\"" ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --enable-password-save \
    --mandir=/usr/share/man \
    --enable-iproute2 \
    --enable-systemd
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  # Install openvpn
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}"/etc/openvpn
  # Install examples
  install -d -m755 "${pkgdir}"/usr/share/openvpn
  cp -r sample/sample-config-files "${pkgdir}"/usr/share/openvpn/examples
  # Install license
  install -d -m755 "${pkgdir}"/usr/share/licenses/${pkgname}/
  ln -sf /usr/share/doc/${pkgname}/{COPYING,COPYRIGHT.GPL} "${pkgdir}"/usr/share/licenses/${pkgname}/
  # Install contrib
  install -d -m755 "${pkgdir}"/usr/share/openvpn/contrib
  cp -r contrib "${pkgdir}"/usr/share/openvpn
  # Install systemd service
  install -D -m644 "${srcdir}"/openvpn@.service "${pkgdir}"/usr/lib/systemd/system/openvpn@.service
}
